plugins {
    id 'com.herodevs.nes.springframework.boot' version '2.4.13-spring-boot-2.4.13-trial'
//    id 'org.springframework.boot' version '2.5.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'jacoco'
    id "io.freefair.lombok" version "5.3.3.3"
    id "org.ec4j.editorconfig" version "0.0.3"
    id "org.sonarqube" version "3.1.1"
    id 'com.google.cloud.tools.jib' version '3.1.4'
}

group = 'io.github.raeperd'
version = '2.1.1'
sourceCompatibility = '11'

repositories {
    mavenCentral()
    maven {
        url 'https://maven.pkg.github.com/neverendingsupport/spring-boot'
        credentials {
            username = System.getenv("GITHUB_USER")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.mockito:mockito-inline:3.12.1'
}

apply from: 'test.gradle'

test {
    useJUnitPlatform()
}

sonarqube {
    properties {
        property "sonar.projectKey", "raeperd_realworld-springboot-java"
        property "sonar.organization", "raeperd"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

jib {
    from {
        image = "openjdk:11.0.10-jre-buster"
        platforms {
            platform {
                architecture = 'amd64'
                os = 'linux'
            }
            platform {
                architecture = 'arm64'
                os = 'linux'
            }
        }
    }
    to {
        image = "ghcr.io/raeperd/${rootProject.name}"
    }
    container {
        ports = ["8080"]
    }
}
